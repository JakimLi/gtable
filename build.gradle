apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'codenarc'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

group = 'jakim'
version = '1.0.0'
sourceCompatibility = 1.6

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

codenarc {
    configFile = file("${project.projectDir}/config/codenarc/codenarc.groovy")
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.codehaus.groovy:groovy-all:2.1.5',
            'com.google.guava:guava-collections:r03'
    )

    testCompile(
            'junit:junit:4.5',
            'org.spockframework:spock-maven:0.7-groovy-2.0',
            'org.objenesis:objenesis:2.1',
            'cglib:cglib-nodep:3.1',
            'mysql:mysql-connector-java:5.1.27'
    )
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = 'jakimli'
    key = ''

    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        userOrg = 'jakimli'
        name = 'gtable'
        websiteUrl = 'https://github.com/JakimLi/gtable'
        issueTrackerUrl = 'https://github.com/JakimLi/gtable/issues'
        vcsUrl = 'https://github.com/JakimLi/gtable.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'light-weight orm', 'crud']
        publicDownloadNumbers = true
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = 2.0;
}
